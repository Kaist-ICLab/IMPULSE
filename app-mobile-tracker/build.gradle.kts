import java.util.Properties

plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.kotlinCompose)
    alias(libs.plugins.googleServices)

    id("com.google.devtools.ksp")
    alias(libs.plugins.kotlinSerialization)

    // ObjectBox (uses kapt; must be applied after the Android + Kotlin plugins).
    alias(libs.plugins.kapt)
    alias(libs.plugins.objectbox)
}

kotlin {
    jvmToolchain(17)
}

android {
    namespace = "kaist.iclab.mobiletracker"
    compileSdk = libs.versions.compileSdk.get().toInt()

    lint {
        abortOnError = false
        disable.add("ExtraTranslation")
    }

    defaultConfig {
        applicationId = "kaist.iclab.trackerSystem"
        minSdk = libs.versions.minSdk.get().toInt()
        targetSdk = libs.versions.targetSdk.get().toInt()
        versionCode = 1
        versionName = "1.0.0"

        // Load local.properties for local development
        val localProperties = Properties()
        val localPropertiesFile = rootProject.file("local.properties")
        if (localPropertiesFile.exists()) {
            localPropertiesFile.inputStream().use { localProperties.load(it) }
        }

        // Supabase credentials: read from local.properties or environment (for CI)
        val supabaseUrl: String = findProperty("SUPABASE_URL")?.toString()
            ?: localProperties.getProperty("SUPABASE_URL")
            ?: System.getenv("SUPABASE_URL")
            ?: "MISSING_SUPABASE_URL"

        val supabaseAnonKey: String = findProperty("SUPABASE_ANON_KEY")?.toString()
            ?: localProperties.getProperty("SUPABASE_ANON_KEY")
            ?: System.getenv("SUPABASE_ANON_KEY")
            ?: "MISSING_SUPABASE_ANON_KEY"

        buildConfigField("String", "SUPABASE_URL", "\"$supabaseUrl\"")
        buildConfigField("String", "SUPABASE_ANON_KEY", "\"$supabaseAnonKey\"")

        // Google Auth web client id: read from local.properties or environment (for CI).
        // Exposed as a string resource because both AndroidManifest and CredentialManager
        // consume it as @string/default_web_client_id.
        val webClientId: String = findProperty("WEB_CLIENT_ID")?.toString()
            ?: localProperties.getProperty("WEB_CLIENT_ID")
            ?: System.getenv("WEB_CLIENT_ID")
            ?: "MISSING_WEB_CLIENT_ID"

        resValue("string", "default_web_client_id", webClientId)
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    sourceSets.getByName("androidTest") {
        assets.directories.add("schemas")
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    signingConfigs {
        getByName("debug") {
            storeFile = project.file("debug.keystore")
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
    }

    buildTypes {
        getByName("debug") {
            signingConfig = signingConfigs.getByName("debug")
        }
        release {
            signingConfig = signingConfigs.getByName("debug")
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    buildToolsVersion = libs.versions.buildTools.get()
}

androidComponents {
    onVariants { variant ->
        variant.outputs.forEach { output ->
            output.outputFileName.set("EnPULSE-Mobile.apk")
        }
    }
}

dependencies {
    /* Android Tracker Library */
    implementation(project(":tracker-library"))

    /* Couchbase Lite - tracker-library exposes CouchbaseDB but not the underlying com.couchbase.lite
       types, needed directly here by CouchbaseWebAppStorage */
    implementation(libs.couchbase)

    /* Android Compose */
    implementation(libs.compose.activity)
    implementation(libs.compose.ui)
    implementation(libs.compose.ui.graphics)
    implementation(libs.compose.ui.tooling.preview)
    implementation(libs.compose.material3)

    implementation(libs.compose.material)
    implementation(libs.compose.material.icons.extended)

    implementation(platform(libs.compose.bom))
    debugImplementation(libs.compose.ui.tooling)

    /* Androidx */
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.service)
    implementation(libs.compose.lifecycle.viewmodel)

    /* Navigation */
    implementation(libs.compose.navigation)

    /* Supabase Related */
    implementation(libs.supabase.kt)
    implementation(libs.supabase.auth.kt)
    implementation(libs.postgrest.kt)
    implementation(libs.functions.kt)
    implementation(libs.realtime.kt)
    implementation(libs.storage.kt)
    implementation(libs.ktor.client.okhttp)
    implementation(libs.ktor.client.core)

    /* Google Authentication (for Supabase Auth) */
    implementation(libs.androidx.credentials)
    implementation(libs.androidx.credentials.auth)
    implementation(libs.googleid)

    /* Security - Encrypted SharedPreferences */
    implementation(libs.androidx.security.crypto)

    /* WebView bridge (WebMessageListener) for EnPULSE webapps */
    implementation(libs.androidx.webkit)

    /* Kotlin Serialization */
    implementation(libs.kotlinx.serialization.json)

    /* Koin Dependency Injection */
    implementation(libs.koin.android)
    implementation(libs.koin.androidx.compose)

    // ObjectBox (applied via the io.objectbox plugin, which adds the runtime + kapt processor)

    /* Google Play Services Wearable */
    implementation(libs.android.gms.wearable)
    implementation(libs.kotlinx.coroutines.play.services)

    /* Google Play Services Location */
    implementation(libs.android.gms.location)

    /* Testing */
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
}
